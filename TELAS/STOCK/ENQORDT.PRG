* PROGRAMA ENQORDT
* INFORMA EN QUE ORDEN ABIERTA HAY PENDIENTE UNA TELA DETERMINADA
* MODIFICADO 09/08/95 11:08 PARA QUE INFORME QUE NO HAY NADA
* MODIFICADO 09/08/95 11:08 PARA USAR SORT Y ACELERAR EL PROCESO
* MODIFICADO 09/08/95 11:08 PARA UNIFICAR EN ESTE TELAS,AVIOS,PTERM Y O.PROD
PROCEDURE ENQORDT
	PARAMETER ENQ_TIP,EL_CODIGO
	LOSPARAM=PARAMETER()
	#DEFINE BOTOM  "  "+repl("°",78)              && BOTOM
	#DEFINE NUL    0                              && NUL
	#DEFINE CRLF CHR(13)+CHR(10)                  && CRLF
	#DEFINE ENTERCON "PULSE [ENTER] PARA CONTINUAR" && ENTERCON

	EXTERNAL PROCEDURE ELIAVIS
	EXTERNAL PROCEDURE ELITELS
	EXTERNAL PROCEDURE ELICOMS
	EXTERNAL PROCEDURE ELIOTRS

	=OPMIX()
	=OPREQ()
	FISC="!REQ.COMPLETO .AND. "
	DO CASE
	CASE ENQ_TIP="A"
		=OPAVIS()
		ELEGIR='ELIAVIS("A BUSCAR",-1,1)'
		EL_ALIAS="AVIS"
		EL_TEXTO="AVIOS"
		EL_CODIGO="ALLTRIM(AVIS.AVICODPRO)"
		EL_CONTENIDO="alltrim(AVIS.AVIO)"
		FISC=FISC+';
			REQX.AVICODPRO=AVIS.AVICODPRO'
	CASE ENQ_TIP="T"
		=OPTELS()
		ELEGIR='ELITELS("A BUSCAR",-1,1)'
		EL_ALIAS="TELS"
		EL_TEXTO="TELAS"
		EL_CODIGO="ALLTRIM(TELS.TELCODPRO)"
		EL_CONTENIDO="alltrim(TELS.TELA)"
		FISC=FISC+';
			REQX.TELCODPRO=TELS.TELCODPRO'
	CASE ENQ_TIP="C"
		=OPCOMS()
		ELEGIR='ELICOMS("A BUSCAR",-1,1)'
		EL_ALIAS="COMS"
		EL_TEXTO="PRODUCTOS TERMINADOS"
		EL_CODIGO="ALLTRIM(COMS.COMCODPRO)"
		EL_CONTENIDO="alltrim(COMS.COMPRA)"
		FISC=FISC+';
			REQX.COMCODPRO=COMS.COMCODPRO'
	CASE ENQ_TIP="O"
		=OPOTRS()
		ELEGIR='ELIOTRS("A BUSCAR",-1,1)'
		EL_ALIAS="OTRS"
		EL_TEXTO="OTROS PRODUCTOS"
		EL_CODIGO="ALLTRIM(OTRS.OTRCODPRO)"
		EL_CONTENIDO="alltrim(OTRS.OTRO)"
		FISC=FISC+';
			REQX.OTRCODPRO=OTRS.OTRCODPRO'
	ENDCASE
	FISC=FISC+" .AND.	REQX.RECIBIDO<REQX.PEDIDO"
	STORE NUL TO RESPUESTA
	IF LOSPARAM=2 .OR. EVAL(ELEGIR)
		=WORKING()
		=DOTALK()
		TMP=GENTMP()
		ON KEY LABEL TAB KEYB CHR(23)
		SELE REQ
		SET ORDER TO TAG NUMERO
		SELE REQX
		SET ORDER TO
		SET RELA TO NUMERO INTO REQ
		SORT ON NUMERO TO (TMP) FOR &FISC
		=PRXAREA(.T.)
		USE (TMP) ALIAS TMP
		IF !EMPTY(RECC())
			=MEN_LIN("[TAB] O [ESC] Para Salir")
			SET RELA TO NUMERO INTO REQ
			=POPCARTE(CRLF+;
				" ORDENES DE REQUERIMIENTO PENDIENTES "+;
				CRLF+;
				EL_TEXTO+;
				CRLF+;
				EVAL(EL_CODIGO)+;
				CRLF+;
				EVAL(EL_CONTENIDO))
			=MOVECENT(0,0,10,49,"WIN"," Ordenes de Requerimiento Pendientes ")
			BROW FIEL ;
				NUMERO:H="ORDEN N§":P="99999999" ,;
				PEDIDO:H="C. PEDIDA":P="9999999999",;
				RECIBIDO:H="C. RECIB.":P="9999999999",;
				UNIDAD=SAYMIX(TMP.UNIDAD,"MDDCOD"):P="!!!!!!",;
				REQ.FECHAPROM:H="PROMESA" ;
				WIND WIN ;
				NOMENU NODELETE NOAPPEND NOEDIT
			=OFF_RELA()
			=RELEWIN("WIN")
			=RELEWIN("POPCARTE")
			ON KEY LABEL TAB
		ELSE
			=CARTEL("NO HAY ORDENES DE REQUERIMIENTOS PENDIENTES",;
				"DE "+EL_TEXTO+" "+EVAL(EL_CODIGO),;
				EVAL(EL_CONTENIDO),;
				ENTERCON)
		ENDIF
		=UNTALK()
		=UNWORK()
		=OFF_TMP()
	ENDIF
RETURN MEN_LIN(BOTOM)
