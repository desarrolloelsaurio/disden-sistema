#DEFINE NUL    0                                 && NUL
#DEFINE BOTOM  "  "+repl("°",78)                 && BOTOM
FUNCTION GENREMI
	=OPKARS()
	SCATT MEMV BLANK
	=OPREM()
	=OPRPV()
	=OPREQ(.F.,.F.,.T.)
	=OPCOR()
	=OPPEN()
	=OPTALS()
	=OPMER()
	=OPAVIS()
	DO WHILE .T.
		NROOCOR=INFIC(99999," N§ de Orden de Corte ")
		IF EMPTY(NROOCOR) .OR. LAST()=27
			EXIT
		ENDIF
		IF OPOCO(NROOCOR)
			COUNT FOR EMPTY(AVICODPRO) TO CUANTOBL
			DO WHILE !EMPTY(CUANTOBL)
				WAIT "FALTAN CARGAR CODIGOS DE AVIOS !!" WIND
				DO CHANGAVI
				COUNT FOR EMPTY(AVICODPRO) TO CUANTOBL
				IF LAST()=27
					EXIT
				ENDIF
			ENDDO
			IF EMPTY(CUANTOBL)
				INDE ON AVICODPRO TAG AVICODPRO
				SET ORDE TO TAG AVICODPRO
				PUSH KEY CLEAR
				ON KEY LABEL TAB KEYBOARD "{Ctrl+W}"
				=MOVEWIN(2,0,13,78,"XXGENREMI"," Orden de Corte N§ "+alltrim(str(NROOCOR))+' ')
				GO TOP
				STORE "" TO M.TALELE, M.CRTERR
				BROW FIEL ;
					AVICODPRO:H='ART.S/COMPRAS':20,;
					ZA=LOOKUP(AVIS.AVIO, OCO.AVICODPRO, AVIS.AVICODPRO, "AVICODPRO"):H='DESCRIP. COMPRAS':15, ;
					CANTIDAD:H='CANTIDAD':P='9999999.9999', ;
					ENVIADA:H='ENVIADO':P='9999999.9999':V=VALENVIO():E=M.CRTERR, ;
					TALLER:H='TALLER':10 , ;
					MEDIDA:H='UNIDAD':5 ;
					FREEZE ENVIADA IN WIND XXGENREMI NOMENU NOCLEA ;
					WHEN WNCL("STOCK: "+;
					TRANS(LOOKUP(AVIS.STOCK, OCO.AVICODPRO, AVIS.AVICODPRO, "AVICODPRO"),"9,999,999.99"))
				COUNT FOR !EMPTY(ENVIADA) TO CUANTOBL
				IF LAST()#27 .AND. !EMPTY(CUANTOBL)
					M.REMNRO=GETNU1("REMITOF","FOXGRL")+1
					M.REMITO='M0000-'+ZERO(M.REMNRO,8)
					STORE DATE() TO M.FECHAOP
					STORE ''     TO M.DETALLE
					STORE M.TALELE TO M.TALLER
					DO GENREMI.SPR WITH .T.
					IF LAST()#27 .AND. CONFIRMA("El Remito", .T., 20, 60)
					    =ANULRES(NROOCOR)
						M.HAYUNO = .F.
						SELE OCO
						SCAN
							M.CANTIDAD=CANTIDAD-ENVIADA
							M.NUMERO=NROOCOR
							M.AVICODPRO=AVICODPRO
							IF M.CANTIDAD > NUL
								**** PASA A PENDIENTES.DBF
								M.TALLER=TALCOD
								=AGRABAR('PEN')
								*** BATA 20-12-95     PONE LA RESERVA DE PENDIENTES 
								SELE AVIS
								IF XSEEK(M.AVICODPRO, "AVIS", "AVICODPRO")
									REPL STKRESPEN WITH STKRESPEN+M.CANTIDAD
									=REPUSE()
								ENDIF
								SELE OCO
								*** FIN BATA 20-12-95     
							ENDIF
							M.CANTIDAD=ENVIADA
							IF !EMPTY(M.CANTIDAD)
								*** PASA A REMITDAF.DBF
								M.TIPO='A'
								=AGRABAR('REX')
								M.HAYUNO = .T.
								*** PASA A KARDEX.DBF
								STORE M.CANTIDAD TO M.SALIDA
								M.COMPR='RF'
								M.NUMERO=ZERO(M.REMNRO,8)
								M.REFERENCIA="O.C. N§ "+ZERO(NROOCOR, 8)
								=AGRABAR("KARS")
								SELE KARS
								*** AJUSTA EL STOCK
				                =AJSTOCK(NUL - M.CANTIDAD,1)
								=IAUD(AVICODPRO+"/"+ALLT(STR(M.CANTIDAD)))
							ENDIF
							*** BATA 20-12-95     SACA LA RESERVA DE LO ENVIADO
							SELE AVIS
							IF XSEEK(STR(NROOCOR)+AVICODPRO, "RPV", "NUMAVIO")
								REPL STOCKRES WITH STOCKRES-RPV.CANTIDAD
								=REPUSE()
								SELE RPV
								DELE
							ENDIF
							*** FIN BATA 20-12-95     
							SELE OCO
						ENDSCAN
						IF M.HAYUNO
							*** PASA A REMITOF.DBF
							STORE M.TALELE TO M.TALLER
							M.NUMOT=NROOCOR
							=AGRABAR('REM')
						ENDIF
						=PUTNU1("REMITOF",M.REMNRO,"FOXGRL")
						M.BASE = DBF('OCO')
						M.BASE=LEFT(M.BASE,RAT(".",M.BASE))
						USE
						ERASE (M.BASE+"DBF")
						ERASE (M.BASE+"CDX")
						*** IMPRIME EL REMITO
						DO LREMIC WITH M.REMITO
					ENDIF
				ENDIF
				IF USED('OCO')
					SELE OCO
					REPL ALL ENVIADA WITH NUL
				ENDIF
				=RELEWIN("XXGENREMI")
				=RELEWIN("GENREMI")
				=MEN_LIN(BOTOM)
				POP KEY
			ENDIF
		ELSE
			WAIT "No Existe la Orden de Corte N§ "+ALLTR(STR(NROOCOR)) WIND NOWA
		ENDIF
	ENDDO
	=CLOSDBF("REQX")
	USE IN REX
	USE IN REM
RETURN .T.

FUNCTION VALENVIO
IF ENVIADA>CANTIDAD*1.1
	M.CRTERR="Enviado no puede ser MAYOR que lo pedido"
	REPL ENVIADA WITH NUL
	RETURN !SGET("XXGENREMI")
ENDIF
IF EMPTY(M.TALELE)
	M.TALELE=CODCLI
ENDIF
IF M.TALELE#CODCLI .AND. !EMPTY(ENVIADA)
	M.CRTERR="No puede ser a Distinto TALLER"
	REPL ENVIADA WITH NUL
	RETURN !SGET("XXGENREMI")
ENDIF
RETURN .T.
